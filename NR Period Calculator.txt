(iDate as date)  =>
let
    ddmmFinancialPeriod = "01/04/",
    iYear = Date.Year(iDate),
    i1stApril = Date.FromText(ddmmFinancialPeriod & Number.ToText(iYear)),
    i1stAprilPrevious = Date.FromText(ddmmFinancialPeriod & Number.ToText(iYear-1)),
    iAprilAdjustment = 
        if iDate>=i1stApril 
            then 
                if Date.DayOfWeek(i1stApril)>= 2 
                    then 7-(Date.DayOfWeek(i1stApril)+1) 
                    else -(Date.DayOfWeek(i1stApril)+1)
                else
                    if Date.DayOfWeek(i1stAprilPrevious)>= 2 
                    then 7-(Date.DayOfWeek(i1stAprilPrevious)+1) 
                    else -(Date.DayOfWeek(i1stAprilPrevious)+1),
    periodNumber =
        (
            if 
                Number.IntegerDivide(
                    (
                        if iDate>=i1stApril 
                        then Number.From(iDate-i1stApril) 
                        else Number.From(iDate-i1stAprilPrevious)
                    )- iAprilAdjustment,7*4
                )>12 
            then 12 
            else
                Number.IntegerDivide(
                    (
                        if iDate>=i1stApril 
                        then Number.From(iDate-i1stApril) 
                        else Number.From(iDate-i1stAprilPrevious)
                    )- iAprilAdjustment,7*4
                )
        )+1,
    
    periodName = 
        "Period " & Number.ToText(periodNumber),

    abbreviatedPeriodName = 
        "P" & Number.ToText(periodNumber),

    financialYear = 
        if Date.Month(iDate) < 4 
        then Number.ToText(Date.Year(iDate)-1) & "/" & Number.ToText(Date.Year(iDate))
        else Number.ToText(Date.Year(iDate)) & "/" & Number.ToText(Date.Year(iDate)+1),

    abbreviatedFinancialYear = 
        if Date.Month(iDate) < 4 
        then Number.ToText(Date.Year(iDate)-1) & "/" & Text.End(Number.ToText(Date.Year(iDate)),2)
        else Number.ToText(Date.Year(iDate)) & "/" & Text.End(Number.ToText(Date.Year(iDate)+1),2),

    monthNumber = Date.Month(iDate),
    monthName = Date.MonthName(iDate),
    dayName = Date.DayOfWeekName(iDate),
    dayOfMonth = Date.Day(iDate),

//Added to return Period and Year
    createRecord=
        Record.AddField([],"Period",periodNumber),
    addToRecord1=
        Record.AddField(createRecord,"Period Name",periodName),
    addToRecord2=
        Record.AddField(addToRecord1,"Abbreviated Period Name",abbreviatedPeriodName),
    addToRecord3=
        Record.AddField(addToRecord2,"Financial Year",financialYear),
    addToRecord4=
        Record.AddField(addToRecord3,"Abbreviated Financial Year",abbreviatedFinancialYear),
    addToRecord5=
        Record.AddField(addToRecord4,"Month Number",monthNumber),
    addToRecord6=
        Record.AddField(addToRecord5,"Month Name",monthName),
    addToRecord7=
        Record.AddField(addToRecord6,"Day Name",dayName),
    resultSet=
        Record.AddField(addToRecord7,"Day Of Month",dayOfMonth)

in
    resultSet
